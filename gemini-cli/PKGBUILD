# Maintainer: Mark Wells <m-wells>
# Packaged from npm
pkgname=gemini-cli
pkgver=0.24.4
# renovate: datasource=npm depName=@google/gemini-cli
pkgrel=1
epoch=1
pkgdesc="Open-source AI agent that brings the power of Gemini directly into your terminal"
arch=('any')
url="https://github.com/google-gemini/gemini-cli"
license=('Apache-2.0')
depends=('nodejs')
makedepends=('npm')
options=('!strip' '!debug' '!lto')
source=("https://registry.npmjs.org/@google/gemini-cli/-/${pkgname}-${pkgver}.tgz")
noextract=("${pkgname}-${pkgver}.tgz")
sha256sums=('44ec58bb628952a72879399790257bf823dcc6c803d6e94f5df0aa3b949eae7d')

package() {
  npm install -g --prefix "${pkgdir}/usr" \
    --cache "${srcdir}/npm-cache" \
    --silent --no-fund --no-audit --no-update-notifier \
    "${srcdir}/${pkgname}-${pkgver}.tgz"

  # Fix permissions
  find "${pkgdir}/usr" -type d -exec chmod 755 {} +

  # Clean npm metadata from package.json files
  find "${pkgdir}" -type f -name package.json -print0 | \
    xargs -0 sed -i -e '/_where/d' -e '/_args/d' -e '/_from/d' -e '/_resolved/d'

  # Remove build artifacts
  find "${pkgdir}" -name config.gypi -delete
  find "${pkgdir}" -name Makefile -delete

  # License
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  if [ -f "${pkgdir}/usr/lib/node_modules/@google/gemini-cli/LICENSE" ]; then
    ln -s "../../../lib/node_modules/@google/gemini-cli/LICENSE" \
      "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  fi
}
