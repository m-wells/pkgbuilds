# Maintainer: Mark Wells <m-wells>
pkgname=rpi-imager
pkgver=2.0.3
# renovate: datasource=github-releases depName=raspberrypi/rpi-imager
pkgrel=9
pkgdesc="Raspberry Pi Imaging Utility (AppImage)"
arch=('x86_64')
url="https://github.com/raspberrypi/rpi-imager"
license=('Apache-2.0')
depends=('fuse2' 'zlib' 'hicolor-icon-theme' 'polkit')
provides=('rpi-imager')
conflicts=('rpi-imager')
options=('!strip' '!debug')
source=("Raspberry_Pi_Imager-v${pkgver}-desktop-x86_64.AppImage::https://github.com/raspberrypi/rpi-imager/releases/download/v${pkgver}/Raspberry_Pi_Imager-v${pkgver}-desktop-x86_64.AppImage"
        "com.raspberrypi.rpi-imager.policy::https://raw.githubusercontent.com/raspberrypi/rpi-imager/v${pkgver}/debian/com.raspberrypi.rpi-imager.policy")
sha256sums=('df1f22628d4a9e0c5cf404dc48cbd31fd38a1b676d70505e23a7c866b24935ab'
            'f998b4c8da419da3a453a3b8992397fb4b8687b7155eda1c79aa0d7a0eb4e524')
noextract=("Raspberry_Pi_Imager-v${pkgver}-desktop-x86_64.AppImage")

package() {
  # Install AppImage
  install -Dm755 "${srcdir}/Raspberry_Pi_Imager-v${pkgver}-desktop-x86_64.AppImage" \
    "${pkgdir}/opt/${pkgname}/${pkgname}.AppImage"

  # Create runuser wrapper (clears AppImage's LD_LIBRARY_PATH to fix PAM issues)
  install -dm755 "${pkgdir}/opt/${pkgname}/bin"
  cat > "${pkgdir}/opt/${pkgname}/bin/runuser" << 'EOF'
#!/bin/sh
# Clear library paths that AppImage sets - they break PAM in system binaries
unset LD_LIBRARY_PATH
unset LD_PRELOAD
exec /usr/bin/runuser "$@"
EOF
  chmod 755 "${pkgdir}/opt/${pkgname}/bin/runuser"

  # Create runner script (sets PATH for xdg-open wrapper)
  cat > "${pkgdir}/opt/${pkgname}/run.sh" << 'EOF'
#!/bin/sh
export PATH="/opt/rpi-imager/bin:$PATH"
exec /opt/rpi-imager/rpi-imager.AppImage "$@"
EOF
  chmod 755 "${pkgdir}/opt/${pkgname}/run.sh"

  # Create launcher script (uses pkexec for privilege elevation)
  install -dm755 "${pkgdir}/usr/bin"
  cat > "${pkgdir}/usr/bin/${pkgname}" << 'EOF'
#!/bin/sh
pkexec --disable-internal-agent /opt/rpi-imager/run.sh "$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"

  # Extract icon and desktop file from AppImage
  cd "${srcdir}"
  chmod +x "Raspberry_Pi_Imager-v${pkgver}-desktop-x86_64.AppImage"
  "./Raspberry_Pi_Imager-v${pkgver}-desktop-x86_64.AppImage" --appimage-extract

  # Install desktop file
  install -Dm644 squashfs-root/usr/share/applications/com.raspberrypi.rpi-imager.desktop \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  sed -i "s|Exec=.*|Exec=/usr/bin/${pkgname}|g" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  install -Dm644 squashfs-root/usr/share/icons/hicolor/scalable/apps/rpi-imager.svg \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"

  # Install polkit policy for privilege elevation (with fixed path for runner script)
  install -Dm644 "${srcdir}/com.raspberrypi.rpi-imager.policy" \
    "${pkgdir}/usr/share/polkit-1/actions/com.raspberrypi.rpi-imager.policy"
  sed -i 's|/usr/bin/rpi-imager|/opt/rpi-imager/run.sh|g' \
    "${pkgdir}/usr/share/polkit-1/actions/com.raspberrypi.rpi-imager.policy"
}
