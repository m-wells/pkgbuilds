# Maintainer: Mark Wells <contact@markwells.dev>
# shellcheck shell=bash disable=SC2034,SC2154
pkgname=python-jsonpath-rw
pkgver=1.4.0
pkgrel=1
pkgdesc="JSONPath implementation for Python"
arch=('any')
url="https://github.com/kennknowles/python-jsonpath-rw"
license=('Apache-2.0')
depends=(
    'python'
    'python-ply'
    'python-decorator'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
)
source=("https://files.pythonhosted.org/packages/source/j/jsonpath-rw/jsonpath-rw-$pkgver.tar.gz")
sha512sums=('5fcbd832975a32d375fd400ebfb84f083c0e7d8780cc63df730a75cca0224bf32d72cb793fcac63fc3afa9697199d7a569af29961da95494253996cc6e5f63b7')

build() {
    cd "jsonpath-rw-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "jsonpath-rw-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
# vim:set ts=2 sw=2 et:
