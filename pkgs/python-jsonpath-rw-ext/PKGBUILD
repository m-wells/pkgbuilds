# Maintainer: Mark Wells <contact@markwells.dev>
# shellcheck shell=bash disable=SC2034,SC2154
pkgname=python-jsonpath-rw-ext
pkgver=1.2.2
pkgrel=1
pkgdesc="Extensions for JSONPath RW"
arch=('any')
url="https://github.com/sileht/python-jsonpath-rw-ext"
license=('Apache-2.0')
depends=(
    'python'
    'python-jsonpath-rw>=1.2.0'
    'python-pbr'
)
makedepends=(
    'python-build'
    'python-installer'
    'python-setuptools'
)
source=("https://files.pythonhosted.org/packages/source/j/jsonpath-rw-ext/jsonpath-rw-ext-$pkgver.tar.gz")
sha512sums=('28ca708023a942665c946d3e0ec618488f7d30d832e4ad0748d6c7019849f2ad19e3629c4452ab461b9c14d71f3360a4590357637f95ea44f8bc3b4544d5e0af')

build() {
    cd "jsonpath-rw-ext-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "jsonpath-rw-ext-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
# vim:set ts=2 sw=2 et:
