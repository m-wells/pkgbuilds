# Maintainer: Mark Wells <contact at markwells dot dev>
# Packaged from npm
pkgname=gemini-cli
pkgver=0.27.3
pkgrel=1
epoch=1
pkgdesc="Open-source AI agent that brings the power of Gemini directly into your terminal"
arch=('any')
url="https://github.com/google-gemini/gemini-cli"
license=('Apache-2.0')
depends=('nodejs')
makedepends=('npm')
options=('!strip' '!debug' '!lto')
source=("https://registry.npmjs.org/@google/gemini-cli/-/${pkgname}-${pkgver}.tgz"
    "gemini-wrapper.sh")
noextract=("${pkgname}-${pkgver}.tgz")
sha512sums=('fb0defd4cb6529330c89db3fa7f5605c8da17061719d4a7dbb25105d70a57da031d666bf4de4a1489c1d621ee4b49cc7bbc54df1bdc37c9772a3c6bdfaf62c2d' '6874b34f459bdcf2b78f67447cdf734adb2cc5f2e37229198f1359f187bf799451f77f9aaafc5f08e48f4b98175694affa767abac15e170c2af903ad9489f525')

package() {
    npm install -g --prefix "${pkgdir}/usr" \
        --cache "${srcdir}/npm-cache" \
        --silent --no-fund --no-audit --no-update-notifier \
        "${srcdir}/${pkgname}-${pkgver}.tgz"

    # Fix permissions
    find "${pkgdir}/usr" -type d -exec chmod 755 {} +

    # Clean npm metadata from package.json files
    find "${pkgdir}" -type f -name package.json -print0 |
        xargs -0 sed -i -e '/_where/d' -e '/_args/d' -e '/_from/d' -e '/_resolved/d'

    # Remove build artifacts
    find "${pkgdir}" -name config.gypi -delete
    find "${pkgdir}" -name Makefile -delete

    # License
    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    if [ -f "${pkgdir}/usr/lib/node_modules/@google/gemini-cli/LICENSE" ]; then
        ln -s "../../../lib/node_modules/@google/gemini-cli/LICENSE" \
            "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    fi

    # Replace symlink with wrapper to suppress punycode deprecation warnings (Node 21+)
    if [ -L "${pkgdir}/usr/bin/gemini" ]; then
        rm "${pkgdir}/usr/bin/gemini"
    fi
    install -Dm755 "${srcdir}/gemini-wrapper.sh" "${pkgdir}/usr/bin/gemini"
}
