# Maintainer: envolution
# Contributor: mdaniels5757 <arch at mdaniels dot me>
# Contributor: ejiek <ejiek@mail.ru>
# Contributor: jskier <jay @jskier.com>
# shellcheck shell=bash disable=SC2034,SC2154
pkgname=keeper-commander
pkgver=17.2.0
pkgrel=1
epoch=1
pkgdesc="CLI, SDK and interactive shell for KeeperÂ® Password Manager."
arch=('any')
url="https://github.com/Keeper-Security/Commander"
license=('MIT')
depends=('python'
    'python-asciitree'
    'python-bcrypt'
    'python-colorama'
    'python-prompt_toolkit'
    'python-pycryptodomex'
    'python-pyperclip'
    'python-tabulate'
    'python-fido2'
    'python-requests'
    'keeper-secrets-manager-core>=16.6.0'
    'python-protobuf'
    'python-cryptography'
    'python-pydantic'
    'python-flask-limiter'
    'python-pyngrok'
    'python-dotenv'
    'python-psutil'
    'python-websockets'
    'python-textual'
    'python-flask'
)
optdepends=(
    'python-fpdf2: PDF export support'
    'python-pysocks: SOCKS proxy support'
    'python-aiortc: WebRTC support'
    'google-crc32c: Cloud integration support'
    'python-pykeepass: KeePass support'
    'python-ldap3: LDAP support'
    'python-botocore: AWS support'
    'python-paramiko: SSH/SFTP support'
    'python-pexpect: Automation support'
    'python-google-auth: Google auth support'
    'python-certifi: SSL certificate support'
    'python-boto3: AWS SDK support'
    'python-yaml: YAML support'
    'python-google-api-python-client: Google API support'
)
makedepends=(
    'python-installer'
    'python-build'
    'python-setuptools'
    'python-setuptools-scm'
)
checkdepends=('python-ifaddr' 'python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('0fac36641c8cac880f8d9345aeada20e74f19a6de123ceed2848d85c1d23ed30325027ef81b19f3e095ce6b44c359968ec559e379b755df98b7bf36aa73da699')

build() {
    cd "Commander-$pkgver"
    python -m build --wheel --no-isolation
}

check() {
    cd "Commander-$pkgver"
    python -m pytest -s -v -k \
        "not test_forward_data_to_tunnel_generic_exception and \
     not test_add_enterprise_user and \
     not test_commands and \
     not test_report_commands and \
     not test_quoting and \
     not test_service_status_when_running and \
     not test_vault_reports and \
     not test_command_execution_failure and \
     not test_command_execution_success and \
     not test_integration_command_flow \
     "
}

package() {
    cd "Commander-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
# vim:set ts=2 sw=2 et:
